# Maintainer: Michael Hansen <zrax0111 gmail com>

pkgname=vscode-bin
pkgdesc='Visual Studio Code for Linux'
pkgver=0.1.0
pkgrel=1
arch=('x86_64')
url='https://code.visualstudio.com/'
license=('custom')
makedepends=()
depends=('gtk2' 'gconf')

_pkgguid="0D57186C-834B-463A-AECB-BC55A8E466AE"
_pkgfile="VSCode-linux-x64.zip"
source=("http://download.microsoft.com/download/${_pkgguid:0:1}/${_pkgguid:1:1}/${_pkgguid:2:1}/${_pkgguid}/${_pkgfile}")
sha1sums=('47ca16f0dafe423174eae9c5db6252584e8c6914')

package() {
    install -m 0755 -d "${pkgdir}/opt/VSCode"
    cp -r "${srcdir}"/* "${pkgdir}/opt/VSCode"

    # Don't include the source .zip symlink in the install directory
    rm "${pkgdir}/opt/VSCode/${_pkgfile}"

    # Include symlink in system bin directory
    install -m 0755 -d "${pkgdir}/usr/bin"
    ln -s /opt/VSCode/Code "${pkgdir}/usr/bin/vscode"

    # Install license file
    install -D -m644 "${srcdir}/license.txt" \
            "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
